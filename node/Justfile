set dotenv-load := true

bin_name := `rg "name = \"(.*)\"" -or '$1' Cargo.toml | head -n1`

help:
    @just --list --unsorted

bootstrap:
    npm install

build:
    npm_package_name=simple2fa_node ./node_modules/.bin/cargo-cp-artifact -nc index.node -- cargo build --message-format=json-render-diagnostics
alias b := build

run:
    node -i -e "$(< script/include.js)"

alias r := run

release:
    npm_package_name=simple2fa_node ./node_modules/.bin/cargo-cp-artifact -nc index.node -- cargo build --message-format=json-render-diagnostics --release

alias install := release

test:
    cargo test
alias t := test

bench: release
    node script/bench.js


check:
    cargo check

# Bump version. level=major,minor,patch
version level:
    git diff-index --exit-code HEAD > /dev/null || ! echo $(dye -r ERROR) You have untracked changes. Commit your changes before bumping the version.
    cargo bump {{level}}
    cargo update # This bumps Cargo.lock
    git commit -am "Bump {{level}} version"
    VERSION=$(rg  "version = \"([0-9.]+)\"" -or '$1' Cargo.toml | head -n1) && \
        git tag v$VERSION && \
        git push origin v$VERSION
    git push

publish:
    cargo publish

patch: test
    just version patch
    just publish
